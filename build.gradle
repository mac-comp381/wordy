plugins {
    id 'java'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation group: 'org.parboiled', name: 'parboiled-java', version: '1.4.1'
    implementation group: 'org.ow2.asm', name: 'asm', version: '9.1'
    implementation group: 'org.jooq', name: 'joor', version: '0.9.+'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.+'
}

[
    playground: 'wordy.demo.Playground',
    shader: 'wordy.demo.shader.ShaderUI'
].each { taskName, mainClass ->
    task(taskName, dependsOn: 'classes', type: JavaExec) {
        main = mainClass
        classpath = sourceSets.main.runtimeClasspath
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
    afterTest { desc, result -> 
        logger.quiet "[${desc.className}] ${desc.name}: ${result.resultType}"
    }
}

sourceCompatibility = 1.16
targetCompatibility = 1.16

sourceSets.main.java.srcDirs = ['src']
sourceSets.main.resources.srcDirs = ['res']
sourceSets.test.java.srcDirs = ['test']
